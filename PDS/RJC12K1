++USERMOD (RJC12K1)
   /* ICSA MODIFICATION TO HASPCOMM TO ADD TOTAL RECORD COUNTS TO
      THE $DF COMMAND; ADD $XF FORM TO PROVIDE PSEUDO-$DJ OUTPUT
      LINES AS WELL FOR EVERY JOB COUNTED IN THE RECORD COUNT, USING
      ONLY THE RECORDS COUNTED FOR THAT FORM; BREAK THE OUTPUT LINES
      SO THAT ONLY 1 OUTPUT CLASS IS USED ON A LINE; AND OMIT THE
      WTR NAME OF (NONE) IF SO.

      LINES K2334750 TO K2334780 CONTAIN CODE WHICH CAN BE USED
      IF THE JTIP MOD FROM UICC IS ON YOUR SYSTEM.  THIS RECOGNIZES
      AN ADDITIONAL "FETCH" QUEUE.  REMOVE THE COMMENT SYMBOL IF
      YOU HAVE JTIP.                                           */.
++VER (Z038) FMID(EJE1102)
             PRE(AZ29819                                    /* IBM  */
                 RCB05A1                                    /* ICSA */).
++MACUPD (#ICSACOM) ASSEM(HASPCOMM,HASPDOC).
./ CHANGE NAME=#ICSACOM
COMIFLG1 DS    BL1                    ICSA COM FLAGS            RJC12K1
         SPACE 1                                                RJC12K1
COMI1X   EQU   B'10000000'            $X( ) COMMAND             RJC12K1
         SPACE 1                                                RJC12K1
++SRCUPD (HASPCOMM).
./ CHANGE NAME=HASPCOMM
         $COMTAB XF,HASPCJ1A,REDIR=$DF   EXTENDED $DF           RJC12K1 K1721215
HASPCJ1A $COMGRUP XF,DF,OQ,PQ       MORE JOB QUEUE COMMANDS     RJC12K1 K2209700
****************************************************************RJC12K1 K2209900
*                                                              *RJC12K1 K2209905
*        $ X F - EXTENDED DISPLAY $ D F                        *RJC12K1 K2209910
*                                                              *RJC12K1 K2209915
****************************************************************RJC12K1 K2209920
CXF      OI    COMIFLG1,COMI1X      SET EXTENDED DISPLAY FLAG   RJC12K1 K2209925
         MVI   COMVERB,C'D'         RESET COMMAND VERB          RJC12K1 K2209930
*        GET SPACE FOR 100 PARTIAL JQE ENTRIES+RECORD COUNTS    RJC12K1 K2209935
*        NUMBER OF ENTRIES BASED ON DF MAXIMUM 100 ENTRY LIMIT  RJC12K1 K2209940
CXFGET   GETMAIN RU,LV=1602,RELATED=(CXFREE,'XF JQE TABLE')     RJC12K1 K2209945
         ST    R1,CXFJQTBL          SAVE ADDRESS OF TABLE       RJC12K1 K2209950
         MVI   COMPNTER,CDFRELJ+CDFHOLD MAKE DEFAULT ALL JOBS   RJC12K1 K2214500
         MVI   CDFIFLG1,0           RESET CHECKLIST FLAGS       RJC12K1 K2216100
         MVI   CDFSSTUP,C' '        BLANK OUT CHECKLIST AREA    RJC12K1 K2216200
         MVC   CDFSSTUP+1(L'CDFSSTUP-1),CDFSSTUP                RJC12K1 K2216300
         L     R0,4(,WD)            POINT TO NEXT OPERAND       RJC12K1 K2218100
         SR    R0,R1                GET SIZE+1                  RJC12K1 K2218200
         SH    R0,=H'4'             GET TEXT MACHINE COUNT      RJC12K1 K2218300
         BM    CDFINVO              INVALID IF NEGATIVE         RJC12K1 K2218400
         $CFSEL (B,CDFB),(C,CDFC),(D,CDFHA),(F,CDFF),(J,CDFJ),(O,CDFO),XK2218500
               (Q,CDFQ),(R,CDFR),(T,CDFT),(W,CDFW) SELECT OPRND RJC12K1 K2218600
         CLI   2(R1),C'R'           THIS DISPLAY RELEASED ONLY? RJC12K1 K2221100
         BE    CDFNSPEC             EQUIVALENT TO DEFAULT       RJC12K1 K2221200
CDFB     OI    CDFIFLG1,CDFI1B      SET TO CHECK BURST          RJC12K1 K2235010
         CH    R0,=H'1'             ONLY Y OR N ALLOWED         RJC12K1 K2235012
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235014
         CLI   2(R1),C'Y'           WAS 'Y' SPECIFIED?          RJC12K1 K2235020
         BNE   CDFSETN              NO, TRY FOR 'N'             RJC12K1 K2235030
         MVI   CDFSB,C'Y'           YES, INSERT INTO CHECKLIST  RJC12K1 K2235040
         B     CDFNSPEC             AND PROCEED TO NEXT OPERAND RJC12K1 K2235050
CDFSETN  CLI   2(R1),C'N'           WAS 'N' SPECIFIED?          RJC12K1 K2235060
         BNE   CDFINVO              NO, INVALID OPERAND         RJC12K1 K2235070
         MVI   CDFSB,C'N'           YES, INSERT INTO CHECKLIST  RJC12K1 K2235080
         B     CDFNSPEC             AND PROCEED TO NEXT OPERAND RJC12K1 K2235090
CDFF     OI    CDFIFLG1,CDFI1F      SET TO CHECK FORMS          RJC12K1 K2235100
         CH    R0,=H'4'             NAMES ARE 4 CHARS OR LESS   RJC12K1 K2235110
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235120
         LR    R15,R0               FOR EXECUTE PURPOSES        RJC12K1 K2235125
         EX    R15,CDFXF            MOVE INTO CHECKLIST         RJC12K1 K2235130
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235140
CDFXF    MVC   CDFSF(*-*),2(R1)     **** EXECUTE ONLY ****      RJC12K1 K2235150
CDFC     OI    CDFIFLG1,CDFI1C      SET TO CHECK CARRIAGE TAPE  RJC12K1 K2235160
         CH    R0,=H'4'             NAMES ARE 4 CHARS OR LESS   RJC12K1 K2235170
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235180
         LR    R15,R0               FOR EXECUTE PURPOSES        RJC12K1 K2235185
         EX    R15,CDFXC            MOVE INTO CHECKLIST         RJC12K1 K2235190
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235200
CDFXC    MVC   CDFSC(*-*),2(R1)     **** EXECUTE ONLY ****      RJC12K1 K2235210
CDFT     OI    CDFIFLG1,CDFI1T      SET TO CHECK TRAIN          RJC12K1 K2235220
         CH    R0,=H'4'             NAMES ARE 4 CHARS OR LESS   RJC12K1 K2235230
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235240
         LR    R15,R0               FOR EXECUTE PURPOSES        RJC12K1 K2235245
         EX    R15,CDFXT            MOVE INTO CHECKLIST         RJC12K1 K2235250
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235260
CDFXT    MVC   CDFST(*-*),2(R1)     **** EXECUTE ONLY ****      RJC12K1 K2235270
CDFW     OI    CDFIFLG1,CDFI1W      SET TO CHECK WRITER NAME    RJC12K1 K2235280
         CH    R0,=H'8'             NAMES ARE 8 CHARS OR LESS   RJC12K1 K2235290
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235300
         LR    R15,R0               FOR EXECUTE PURPOSES        RJC12K1 K2235305
         EX    R15,CDFXW            MOVE INTO CHECKLIST         RJC12K1 K2235310
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235320
CDFXW    MVC   CDFSW(*-*),2(R1)     **** EXECUTE ONLY ****      RJC12K1 K2235330
CDFO     OI    CDFIFLG1,CDFI1O      SET TO CHECK FLASH OVERLAY  RJC12K1 K2235340
         CH    R0,=H'4'             NAMES ARE 4 CHARS OR LESS   RJC12K1 K2235350
         BNL   CDFINVO              IF LONGER, INVALID          RJC12K1 K2235360
         LR    R15,R0               FOR EXECUTE PURPOSES        RJC12K1 K2235365
         EX    R15,CDFXO            MOVE INTO CHECKLIST         RJC12K1 K2235370
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235380
CDFXO    MVC   CDFSO(*-*),2(R1)     **** EXECUTE ONLY ****      RJC12K1 K2235390
CDFQ     OI    CDFIFLG1,CDFI1Q      SET TO USE ONLY THIS CLASS  RJC12K1 K2235400
         CH    R0,=H'1'             ONLY 1 CLASS ALLOWED        RJC12K1 K2235410
         BNL   CDFINVO              IF MORE, INVALID            RJC12K1 K2235420
         L     R14,=A(CLJHLDTB)     CHECK FOR VALID CLASS       RJC12K1 K2235430
         LA    R15,36               THERE ARE 36 CLASSES        RJC12K1 K2235440
CDFQLP   CLC   0(1,R14),2(R1)       TRY                         RJC12K1 K2235450
         BE    CDFXQ                  TO                        RJC12K1 K2235460
         LA    R14,1(,R14)              FIND                    RJC12K1 K2235470
         BCT   R15,CDFQLP                 CLASS IN VALID TABLE  RJC12K1 K2235480
         B     CDFINVO              IF NOT THERE, INVALID       RJC12K1 K2235482
CDFXQ    LA    R14,36               FIND                        RJC12K1 K2235484
         SR    R14,R15                OFFSET                    RJC12K1 K2235486
         SLL   R14,1                    INTO                    RJC12K1 K2235488
         STC   R14,CDFSQ                  TABLE                 RJC12K1 K2235490
         B     CDFNSPEC             PROCEED TO NEXT OPERAND     RJC12K1 K2235492
         TM    CDFIFLG1,X'FF'       ARE WE LIMITING SETUPS?     RJC12K1 K2249510
         BZ    CDFCCHOK             NO, USE ALL OF THEM         RJC12K1 K2249520
         TM    CDFIFLG1,CDFI1B      CHECK BURST?                RJC12K1 K2249530
         BZ    CDFCF                NO, TRY FORMS               RJC12K1 K2249540
         TM    CDFCFLAG,$JOEBRST    BURST=YES?                  RJC12K1 K2249550
         BZ    CDFCBN               NO, CHECK FOR NO            RJC12K1 K2249560
         CLI   CDFSB,C'Y'           WAS 'Y' ASKED FOR?          RJC12K1 K2249570
         BNE   CDFEJOA              NO, SKIP THIS ONE           RJC12K1 K2249580
         B     CDFCF                YES, TRY FORMS              RJC12K1 K2249590
CDFCBN   CLI   CDFSB,C'N'           WAS 'N' ASKED FOR?          RJC12K1 K2249600
         BNE   CDFEJOA              NO, SKIP THIS ONE           RJC12K1 K2249610
CDFCF    TM    CDFIFLG1,CDFI1F      ARE WE LIMITING FORMS?      RJC12K1 K2249620
         BZ    CDFCC                NO, TRY CARRIAGE TAPE       RJC12K1 K2249630
         CLC   CDFORMS,CDFSF        YES, CHECK FORMS SPEC.      RJC12K1 K2249640
         BNE   CDFEJOA              SKIP IF NO MATCH            RJC12K1 K2249650
CDFCC    TM    CDFIFLG1,CDFI1C      ARE WE LIMITING CARR. TAPE? RJC12K1 K2249660
         BZ    CDFCT                NO, TRY PRINT TRAIN         RJC12K1 K2249670
         CLC   CDFFCB,CDFSC         YES, CHECK CARR. TAPE SPEC. RJC12K1 K2249680
         BNE   CDFEJOA              SKIP IF NO MATCH            RJC12K1 K2249690
CDFCT    TM    CDFIFLG1,CDFI1T      ARE WE LIMITING TRAINS?     RJC12K1 K2249700
         BZ    CDFCW                NO, TRY WRITERS             RJC12K1 K2249710
         CLC   CDFUCS,CDFST         YES, CHECK TRAIN SPEC.      RJC12K1 K2249720
         BNE   CDFEJOA              SKIP IF NO MATCH            RJC12K1 K2249730
CDFCW    TM    CDFIFLG1,CDFI1W      ARE WE LIMITING WRITERS?    RJC12K1 K2249740
         BZ    CDFCO                NO, TRY FLASH               RJC12K1 K2249750
         CLC   CDFWTR,CDFSW         YES, CHECK WRITER NAME      RJC12K1 K2249760
         BNE   CDFEJOA              SKIP IF NO MATCH            RJC12K1 K2249770
CDFCO    TM    CDFIFLG1,CDFI1O      ARE WE LIMITING FLASH?      RJC12K1 K2249780
         BZ    CDFCCHOK             NO, USE THIS ONE            RJC12K1 K2249790
         CLC   CDFLASH,CDFSO        YES, CHECK FLASH SPEC.      RJC12K1 K2249800
         BNE   CDFEJOA              SKIP IF NO MATCH            RJC12K1 K2249810
CDFCCHOK NULL  ,                    REJOIN NORMAL CODE          RJC12K1 K2249820
         MVC   CDFMB,=C'F=XXXX C=XXXX T=XXXX L=XXXXX ' BASE MSG RJC12K1 K2252000
         SR    WD,WD                CLEAR LENGTH REGISTER       RJC12K1 K2253600
         MVC   CDFMBW-2(2),=C'W='   INSERT WTR INDICATOR        RJC12K1 K2254600
         LA    WD,L'CDFWTR+2        BUMP LENGTH POINTER         RJC12K1 K2254700
SKIP120  LA    WD,CDFMB+L'CDFMB(WD) POINT TO NEXT MSG AREA      RJC12K1 K2255500
         MVC   CXFMBSV,CDFMID       SAVE SETUP CHARACTERISTICS  RJC12K1 K2261100
         TM    CDFIFLG1,CDFI1Q      ARE WE LIMITING TO ONE CLASSRJC12K1 K2282100
         BZ    CDFCNQ               NO, PROCEED AS USUAL        RJC12K1 K2282200
         IC    WB,CDFSQ             YES, SET THE CLASS OFFSET   RJC12K1 K2282300
         B     CDFACLS              PROCESS CLASS               RJC12K1 K2282400
CDFCNQ   NULL  ,                    REJOIN COMMON CODE          RJC12K1 K2282500
         TM    CDFIFLG1,CDFI1Q      LIMITING TO ONE CLASS?      RJC12K1 K2283600
         BO    CDFEJOE              YES, SKIP THE REST OF THEM  RJC12K1 K2283700
         TM    COMIFLG1,COMI1X      EXTENDED FORM?              RJC12K1 K2293100
         BZ    CXFCJQX              NO, DON'T CLEAR TABLE       RJC12K1 K2293200
         L     R1,CXFJQTBL          GET ADDRESS OF TABLE        RJC12K1 K2293300
         STH   R0,0(,R1)            SET ENTRY COUNTER TO ZERO   RJC12K1 K2293400
CXFCJQX  XC    CDFRECCT,CDFRECCT    CLEAR RECORD COUNT          RJC12K1 K2293500
         MVC   CDFMID(L'CXFMBSV),CXFMBSV REFRESH PRINT LINE     RJC12K1 K2293600
         LR    WE,R14               SAVE FOR LATER USE          RJC12K1 K2300100
         BL    CDFRECSM             GO COUNT AND INSERT IN TABLERJC12K1 K2321000
         L     R0,CDFRECCT          GET TOTAL RECORDS           RJC12K1 K2332100
         CVD   R0,COMDWORK          CONVERT TO DECIMAL          RJC12K1 K2332110
         MVC   COMDWORK-4(8),=X'4020202020202120' EDIT MASK     RJC12K1 K2332120
         ED    COMDWORK-4(8),COMDWORK+4  MAKE PRINTABLE         RJC12K1 K2332130
         MVC   CDFMBL,COMDWORK-1    MOVE INTO PRINT LINE        RJC12K1 K2332140
         C     R0,=F'99999'         100K OR MORE?               RJC12K1 K2332150
         BNH   CXFNKCNT             NO, FINISHED HERE           RJC12K1 K2332160
         MVC   CDFMBL(4),COMDWORK-3 YES, MOVE IN K LINES        RJC12K1 K2332170
         MVI   CDFMBL+4,C'K'        FILL IN "K"                 RJC12K1 K2332180
CXFNKCNT NULL  ,                    REJOIN NORMAL CODE          RJC12K1 K2332190
*        THIS LINE DELETED BY ICSA                              RJC12K1 K2333000
*        THIS LINE DELETED BY ICSA                              RJC12K1 K2333500
         S     WD,=F'5'             BACK UP TO CLASS SLOT       RJC12K1 K2334500
         TM    COMIFLG1,COMI1X      EXTENDED DISPLAY?           RJC12K1 K2334510
         BZ    CXFPJQX              NO, REJOIN NORMAL CODE      RJC12K1 K2334520
         L     WE,CXFJQTBL          GET ADDRESS OF TABLE        RJC12K1 K2334530
         LH    WC,0(,WE)            GET NUMBER OF ENTRIES       RJC12K1 K2334540
         LA    WE,2(,WE)            BUMP TO FIRST ENTRY         RJC12K1 K2334570
         LTR   WC,WC                ARE THERE ANY ENTRIES       RJC12K1 K2334572
         BNP   CXFPJQX              IF NONE, QUIT               RJC12K1 K2334574
CXFPJQLP MVI   COMMAND,C' '         BLANK OUT                   RJC12K1 K2334580
         MVC   COMMAND+1(35),COMMAND   IN USUAL MANNER          RJC12K1 K2334590
         LH    R0,0(,WE)            GET JOBID                   RJC12K1 K2334600
         CH    R0,=H'10000'         IS IT A JOB?                RJC12K1 K2334610
         BNL   CXFPJQT              IF NOT TRY TSU OR STC       RJC12K1 K2334620
         MVC   COMMAND(3),=C'JOB'   INSERT TYPE                 RJC12K1 K2334630
         B     CXFPJQJ              AND CONTINUE                RJC12K1 K2334640
CXFPJQT  SH    R0,=H'20000'         IS IT A TSU?                RJC12K1 K2334650
         BL    CXFPJQS              NO, A STC                   RJC12K1 K2334660
         MVC   COMMAND(3),=C'TSU'   YES, INSERT TYPE            RJC12K1 K2334670
         B     CXFPJQJ                AND CONTINUE              RJC12K1 K2334680
CXFPJQS  MVC   COMMAND(3),=C'STC'   MUST BE STC                 RJC12K1 K2334690
         AH    R0,=H'10000'         READJUST BASE               RJC12K1 K2334700
CXFPJQJ  $CFCVE ,                   CONVERT TO EBCDIC           RJC12K1 K2334710
         MVC   COMMAND+3(5),COMDWORK  INSERT NUMBER             RJC12K1 K2334720
         MVC   COMMAND+9(8),2(WE)   INSERT JOBNAME              RJC12K1 K2334730
         MVC   COMMAND+18(5),=C'Q=PPU'  PRIME WITH QUEUE        RJC12K1 K2334740
*        CLI   14(WE),$FETCH        IS IT IN FETCH QUEUE?       RJC12K1 K2334750
*        BNE   CXFPJQO              NO, SOME OUTPUT QUEUE       RJC12K1 K2334760
*        MVC   COMMAND+20(3),=C'FET'  ADJUST QUEUE TYPE         RJC12K1 K2334770
*        B     CXFPJQL              AND GO INSERT LINES         RJC12K1 K2334780
CXFPJQO  CLI   14(WE),$HARDCPY      IS IT IN PPU QUEUE?         RJC12K1 K2334790
         BE    CXFPJQL              YES, THEN GO ADD LINE       RJC12K1 K2334800
         MVC   COMMAND+20(3),=C'OUT' ADJUST QUEUE TYPE          RJC12K1 K2334810
CXFPJQL  MVC   COMMAND+24(2),=C'L=' ADD LINES KEYWORD           RJC12K1 K2334820
         L     R0,10(,WE)           GET RECORD COUNT            RJC12K1 K2334830
         CVD   R0,COMDWORK          CONVERT TO DECIMAL          RJC12K1 K2334840
         MVC   COMDWORK-4(8),=X'4020202020202120' EDIT MASK     RJC12K1 K2334850
         ED    COMDWORK-4(8),COMDWORK+4  EDIT TO PRINTABLE      RJC12K1 K2334860
         MVC   COMMAND+26(5),COMDWORK-1  MOVE TO PRINT LINE     RJC12K1 K2334870
         C     R0,=F'99999'         100K OR MORE?               RJC12K1 K2334880
         BNH   CXFPKCNT             NO, LEAVE ASIS              RJC12K1 K2334890
         MVC   COMMAND+26(4),COMDWORK-3  MOVE THOUSANDS         RJC12K1 K2334900
         MVI   COMMAND+30,C'K'        AND ADD "K"               RJC12K1 K2334910
CXFPKCNT TM    15(WE),QUEHOLDA+QUEHOLD1  JOB HELD?              RJC12K1 K2334920
         BZ    CXFPJQR              IF NOT, RELEASED            RJC12K1 K2334930
         MVC   COMMAND+32(4),=C'HOLD'  ELSE INSERT FLAG         RJC12K1 K2334940
CXFPJQR  $CWTO L=36                 SEND THE LINE               RJC12K1 K2334950
         LA    WE,16(,WE)           BUMP TO NEXT ENTRY          RJC12K1 K2334960
         BCT   WC,CXFPJQLP          END TRY NEXT ENTRY, IF ANY  RJC12K1 K2334970
CXFPJQX  NULL  ,                    REJOIN IBM CODE             RJC12K1 K2334980
*        THIS LINE DELETED BY ICSA                              RJC12K1 K2335000
CDFNOJOP TM    COMIFLG1,COMI1X      EXTENDED DISPLAY?           RJC12K1 K2349600
         BZ    CXFXJQX              NO, REJOIN NORMAL CODE      RJC12K1 K2349700
         L     WF,CXFJQTBL          POINT TO TABLE              RJC12K1 K2349800
CXFREE   FREEMAIN R,LV=1602,A=(WF),RELATED=(CXFGET,'XF JQE TABLE') 12K1 K2349900
CXFXJQX  TM    CDFFLAGS,CDFJOEOK    DID WE FIND ANY?            RJC12K1 K2350000
CDFRECSM L     R15,CDFRECCT         UPDATE                      RJC12K1 K2357510
         A     R15,JOERECCT           CURRENT                   RJC12K1 K2357520
         ST    R15,CDFRECCT             RECORD TOTAL            RJC12K1 K2357530
         TM    COMIFLG1,COMI1X      EXTENDED DISPLAY?           RJC12K1 K2357540
         BZ    CDFNXTJO             IF NOT, GET NEXT JOE        RJC12K1 K2357550
         LR    R14,WE               RETRIEVE JQE POINTER        RJC12K1 K2357560
         L     WE,CXFJQTBL          GET TABLE ADDRESS           RJC12K1 K2357570
         LH    WC,0(,WE)              AND CURRENT ENTRY COUNT   RJC12K1 K2357580
         LA    WE,2(,WE)            BUMP TO FIRST ENTRY         RJC12K1 K2357590
         LTR   WC,WC                IS TABLE EMPTY?             RJC12K1 K2357592
         BZ    CXFJQIF              YES, JUST INSERT ENTRY      RJC12K1 K2357594
CXFJQILP CLC   0(2,WE),QUEJOBNO(R14)  IS JOB IN TABLE?          RJC12K1 K2357600
         BNE   CXFNXTJQ             SKIP ENTRY IF NOT           RJC12K1 K2357610
         L     R15,10(,WE)          UPDATE CURRENT              RJC12K1 K2357620
         A     R15,JOERECCT           LINE COUNT                RJC12K1 K2357630
         ST    R15,10(,WE)              AND STORE NEW COUNT     RJC12K1 K2357640
         B     CDFNXTJO             THEN REJOIN NORMAL CODE     RJC12K1 K2357650
CXFNXTJQ LA    WE,16(,WE)           BUMP TO NEXT ENTRY          RJC12K1 K2357660
         BCT   WC,CXFJQILP          AND TRY AGAIN               RJC12K1 K2357670
CXFJQIF  MVC   0(2,WE),QUEJOBNO(R14)  NEW ENTRY: INSERT JOBID   RJC12K1 K2357680
         MVC   2(8,WE),QUEJNAME(R14) JOBNAME                    RJC12K1 K2357690
         MVC   10(4,WE),JOERECCT     RECORD COUNT               RJC12K1 K2357700
         MVC   14(1,WE),QUETYPE(R14) QUEUE TYPE                 RJC12K1 K2357710
         MVC   15(1,WE),QUEFLAGS(R14) AND HOLD FLAGS            RJC12K1 K2357720
         L     WE,CXFJQTBL          FIND BEGINNING AGAIN        RJC12K1 K2357730
         LH    WC,0(,WE)            UPDATE                      RJC12K1 K2357740
         LA    WC,1(,WC)              ENTRY                     RJC12K1 K2357750
         STH   WC,0(,WE)                COUNT                   RJC12K1 K2357760
         B     CDFNXTJO             REJOIN NORMAL CODE          RJC12K1 K2357770
CDFMB    EQU   CDFMR1+10,29         BASE TEXT                   RJC12K1 K2399500
CDFMBL   EQU   CDFMBT+7,5           RECORD COUNT                RJC12K1 K2401100
CDFMBW   EQU   CDFMBL+8,8           WRITER NAME                 RJC12K1 K2401500
CDFICSA  EQU   COMMAND+103          ICSA AREA IN COMMAND BUFFER RJC12K1 K2402600
CDFIFLG1 EQU   CDFICSA,1            ICSA FLAGS FOR $ DF         RJC12K1 K2402650
CDFI1B   EQU   B'10000000'          LIMIT BURST                 RJC12K1 K2402700
CDFI1F   EQU   B'01000000'                FORMS                 RJC12K1 K2402750
CDFI1C   EQU   B'00100000'                CARRIAGE TAPE         RJC12K1 K2402800
CDFI1T   EQU   B'00010000'                PRINT TRAIN           RJC12K1 K2402850
CDFI1W   EQU   B'00001000'                WRITER                RJC12K1 K2402900
CDFI1O   EQU   B'00000100'                FLASH                 RJC12K1 K2403000
CDFI1Q   EQU   B'00000001'                CLASS                 RJC12K1 K2403150
CDFSSTUP EQU   CDFIFLG1+1,26        LIMIT CHECKLIST AREA        RJC12K1 K2403200
CDFSB    EQU   CDFSSTUP,1           DESIRED BURST               RJC12K1 K2403250
CDFSF    EQU   CDFSB+1,4                    FORMS               RJC12K1 K2403300
CDFSC    EQU   CDFSF+4,4                    CARRIAGE TAPE       RJC12K1 K2403350
CDFST    EQU   CDFSC+4,4                    TRAIN               RJC12K1 K2403400
CDFSW    EQU   CDFST+4,8                    WRITER              RJC12K1 K2403450
CDFSO    EQU   CDFSW+8,4                    FLASH               RJC12K1 K2403500
CDFSQ    EQU   CDFSO+4,1                    CLASS               RJC12K1 K2403550
CDFRECCT EQU   CDFSSTUP+26,4        CURRENT RECORD COUNT TOTAL  RJC12K1 K2403600
CXFJQTBL EQU   CDFRECCT+4,4         ADDRESS OF JQE TABLE        RJC12K1 K2403650
CXFMBSV  EQU   CXFJQTBL+4,36        REFRESH BUFFER FOR PRINTLINERJC12K1 K2403700
